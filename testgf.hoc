{load_file("nrngui.hoc")}

begintemplate Cell
public soma, syn
create soma
objref syn
proc init() {
  soma {
    nseg=5
    diam = 10  L = 100
    insert hh
    syn = new Gfluct3(.5)
    syn.on = 1
    syn.E_e = 20
    syn.h = 0.5
    syn.noiseFromRandom123(1,2,3)
  }
}

endtemplate Cell

ncell = 100
objref pc, cell, cells, nc, netcons, st, stims, nil
pc = new ParallelContext()

cells = new List()

for (gid = pc.id; gid < ncell; gid += pc.nhost) {
  pc.set_gid2node(gid, pc.id)
  cell = new Cell()
  cell.soma pc.cell(gid, new NetCon(&cell.soma.v(.5), nil))
  cell.syn.noiseFromRandom123(gid,2,3)
  cells.append(cell)
}

objref tvec, idvec
tvec = new Vector()
idvec = new Vector()
pc.spike_record(-1, tvec, idvec)

cvode.cache_efficient(1)
pc.set_maxstep(10)
stdinit()

proc pcell() {localobj s
  s = new String()
  sprint(s.s, "t%g", t)
  pc.prcellstate($1, s.s)
}

pc.nrnbbcore_write("testgfdat")
//pcell(0)
tsav = startsw()
pc.psolve(100)
if (pc.id == 0) { printf("psolve time %g\n", startsw() - tsav) }
//pcell(0)

strdef s
sprint(s, "out%d.dat", pc.id)
objref f
f = new File()
f.wopen(s)
for i=0, tvec.size-1 {
  f.printf("%g %d\n", tvec.x[i], idvec.x[i])
}
f.close()

pc.barrier()
quit()

